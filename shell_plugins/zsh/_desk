#compdef desk
#autoload

emulate -L zsh
set -o extendedglob
local -a desks desktops desks2 listopts

# Change this to the location of the bash script for desk
local deskcmd=${SHELL_SCRIPTS_DIRECTORY}/desk

desks=("${(f)$($deskcmd list)}")
deskopts=()
for desk in "${desks[@]}"; do
    deskopts+=("${desk//(#b)(#s)([^ ]#) #(*)/${match[1]}:${match[2]}:}")
done

_regex_words desktops 'desks' "${deskopts[@]}"
desks2=("${reply[@]}")

_regex_words listopts 'list opts' '--only-names:List only the names of the desks' \
	     '--no-format:Use - to separate names from descriptions'
listopts=("${reply[@]}")

_regex_words subcmd 'subcommands' 'help:Print a help message:' \
	     "${DESK_ENV+\--no-format:Use - to separate alias/export/function names from their descriptions:}" \
	     'init:Initialize your desk configuration:' \
	     'list:List available desks:$listopts' \
	     'edit:Edit or create a desk, defaults to current desk:$desks2' \
	     'go:Activate a desk:$desks2' \
	     'run:Run a command within a desk environment:$desks2' \
	     'version:Show the desk version:'
subcmd=("${reply[@]}")

_regex_arguments _desk /$'[^\0]##\0'/ "${subcmd[@]}"
_desk "$@"

